#Использовать v8runner
#Использовать logos
#Использовать types
#Использовать fs
#Использовать json
#Использовать "./internal"
#Использовать "./internal/timer"

Перем РабочийКонфигуратор;
Перем Лог;

Перем НаборРасширений;

Перем ОбработчикиПередОбновления; // Путь к файлу обработки перед обновлением 
Перем ОбработчикиПослеОбновления; // Путь к файлу обработки после обновлением 
Перем ПутьКОбработкиОбновленияРасширения; // Путь к файлу обработки обновления расширений 
Перем ОтключитьВсеРасширения;

Перем ЗагрузкаРасширенийВКонфигураторе;
Перем РежимУправляемогоПриложения;

Перем МассивСпособовВывода;
Перем ИдентификаторРабочегоПроцесса;

Процедура ПриСозданииОбъекта()
	
	Лог = Логирование.ПолучитьЛог("oscript.app.AutoUpdateIB").Поля("Префикс", "сfe-updater");
	// Лог = Логирование.ПолучитьЛог("oscript.lib.v8runner");
	// Лог.УстановитьУровень(УровниЛога.Отладка);
	ОтключитьВсеРасширения = Ложь;
	МассивСпособовВывода = Новый Массив();

	ОбработчикиПередОбновления = Новый Массив();
	ОбработчикиПослеОбновления = Новый Массив();

КонецПроцедуры

Процедура ДобавитьСпособВывода(Знач ПроцессорВывода) Экспорт
	МассивСпособовВывода.Добавить(ПроцессорВывода);
	Лог.ДобавитьСпособВывода(ПроцессорВывода);
КонецПроцедуры

Процедура УстановитьУправлениеКонфигуратором(НовоеУправлениеКонфигуратором) Экспорт
	РабочийКонфигуратор = НовоеУправлениеКонфигуратором;
КонецПроцедуры

Процедура УстановитьНастройкиОбновления(НастройкиОбновления) Экспорт
	
	НаборРасширений = КопированиеТипа.Скопировать(НастройкиОбновления.НаборРасширений);

	НастройкиОбновления.Свойство("ПутьКОбработкиОбновленияРасширения", ПутьКОбработкиОбновленияРасширения);
	НастройкиОбновления.Свойство("ОтключитьВсеРасширения", ОтключитьВсеРасширения);
	НастройкиОбновления.Свойство("ЗагрузкаРасширенийВКонфигураторе", ЗагрузкаРасширенийВКонфигураторе);
	НастройкиОбновления.Свойство("РежимУправляемогоПриложения", РежимУправляемогоПриложения);
		
	Если НастройкиОбновления.Свойство("ПослеОбновления") Тогда

		ЗаполнитьОбработчик("ПослеОбновления", НастройкиОбновления.ПослеОбновления)

	КонецЕсли;

	Если НастройкиОбновления.Свойство("ПередОбновлением") Тогда

		ЗаполнитьОбработчик("ПередОбновлением", НастройкиОбновления.ПередОбновлением)

	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьРабочийПроцессВЛог(Знач ПИдентификаторРабочегоПроцесса) Экспорт

	Лог = Лог.Поля("РабочийПроцесс", ПИдентификаторРабочегоПроцесса);
	ИдентификаторРабочегоПроцесса = ПИдентификаторРабочегоПроцесса;

КонецПроцедуры

Процедура ПередатьРабочийПроцессВОбъект(Знач ОбъектПриемник)
	
	Если Не ИдентификаторРабочегоПроцесса = Неопределено Тогда
		ОбъектПриемник.ДобавитьРабочийПроцессВЛог(ИдентификаторРабочегоПроцесса);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьОбработчик(ИмяСобытия, МассивОбработчиков)
	
	Если ИмяСобытия = "ПередОбновлением" Тогда
		Обработчики = ОбработчикиПередОбновления;
	ИначеЕсли ИмяСобытия = "ПослеОбновления" Тогда
		Обработчики = ОбработчикиПослеОбновления;
	Иначе
		Возврат;
	КонецЕсли;

	Для каждого Обработчик Из МассивОбработчиков Цикл
			
		ЗапускПредприятия = Новый ЗапускПредприятия;
		ПередатьРабочийПроцессВОбъект(ЗапускПредприятия);
		ЗапускПредприятия.УстановитьУправлениеКонфигуратором(РабочийКонфигуратор);
		Для каждого СпособВывода Из МассивСпособовВывода Цикл
			ЗапускПредприятия.ДобавитьСпособВывода(СпособВывода);
		КонецЦикла;

		ЗапускПредприятия.УстановитьНастройки(Обработчик);

		Обработчики.Добавить(ЗапускПредприятия);

	КонецЦикла;

КонецПроцедуры

Процедура Запустить() Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПутьКОбработкиОбновленияРасширения) Тогда
		
		ИнформацияОбОшибке = НоваяИнформацияОбОшибке("Ошибка обновления расширений не задана обработка обновления");
		Лог.КритичнаяОшибка(ИнформацияОбОшибке);
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецЕсли;

	ВыполнитьОбработчикПриСобытия("ПередОбновлением");
	
	ВыполнитьОбновление();
	
	ВыполнитьОбработчикПриСобытия("ПослеОбновления");
	 
КонецПроцедуры

// TODO: Переделать на обновление через предприятие
Процедура ВыполнитьОбновление()
	ДопПоля = Новый Структура("КоличествоРасширений", НаборРасширений.Количество());
		
	ТрассировкаЗадач = Новый ТрассировкаЗадач(Лог);
	ТрассировкаЗадач.Трассировать("Обновление расширений", "Обновление расширений информационной базы",,,ДопПоля);

	ЗагрузкаРасширенийВыполнена = Ложь;

	Если ЗагрузкаРасширенийВКонфигураторе Тогда
		ЗагрузкаРасширенийВыполнена = ЗагрузкаРасширенийВКонфигураторе();
	КонецЕсли;

	ФайлДанныхДляОбработки = ВременныеФайлы.СоздатьФайл();
	
	ДанныеДляОбработки = СформироватьДанныеДляОбработки(ЗагрузкаРасширенийВыполнена);

	Если ДанныеДляОбработки.НаборРасширений.Количество() = 0 Тогда
		Лог.Предупреждение("Отсутствуют расширения для обновления информационной базы");	
		Возврат;
	КонецЕсли;

	ПутьКФайлуЛога = ВременныеФайлы.СоздатьФайл();

	ДанныеДляОбработки.Вставить("ПутьКФайлуЛога", ПутьКФайлуЛога);

	ЗаписатьДанныеДляОбработки(ФайлДанныхДляОбработки, ДанныеДляОбработки);
	
	КлючЗапуска = СтрШаблон("""%1""", ФайлДанныхДляОбработки);

	ВыполнитьОбработку(ПутьКОбработкиОбновленияРасширения, КлючЗапуска);
	
	Лог.Отладка("Читаю текст лога обновления расширений");
	
	ОбработатьДанныеЛогаОбновленияРасширений(ПутьКФайлуЛога);

	ТрассировкаЗадач.Трассировать("Обновление расширений", "Обновлены расширения информационной базы", "success", , ДопПоля);

КонецПроцедуры

Функция ЗагрузкаРасширенийВКонфигураторе()
	
	// TODO: Реализовать механизм загрузки расширений через конфигуратор
	Возврат Ложь;

КонецФункции

Процедура ОбработатьДанныеЛогаОбновленияРасширений(Знач ПутьКФайлу)
	
	ТекстФайла = ПрочитатьФайл(ПутьКФайлу);

	Лог.Отладка("Текст лога обновления расширений <%1>", ТекстФайла);
	
	СтруктурированныйЛог = ИзJson(ТекстФайла);

	Для каждого СтрокаЛога Из СтруктурированныйЛог Цикл
		
		СообщениеЛога = СтрокаЛога["Сообщение"];
		УровеньСообщения = СтрокаЛога["УровеньСообщения"];
		ДополнительныеДанные = СтрокаЛога["ДополнительныеДанные"];
		
		Если ДополнительныеДанные = Неопределено Тогда
			ДополнительныеДанные = Новый Структура();
		КонецЕсли;
		
		ДатаСобытия = СтрокаЛога["ДатаВремя"];

		ДатаСобытия = ФорматироватьДатуСобытия(ДатаСобытия);
		ЛогСообщения = Лог.Поля("time", ДатаСобытия).ПоляИз(ДополнительныеДанные);

		Если УровеньСообщения = Неопределено Тогда
			УровеньСообщения = УровниЛога.Отладка;
		Иначе
			УровеньСообщения = Число(УровеньСообщения);
		КонецЕсли;

		Если УровеньСообщения = УровниЛога.Информация Тогда
			ЛогСообщения.Информация(СообщениеЛога);
		ИначеЕсли УровеньСообщения = УровниЛога.Ошибка Тогда
			ЛогСообщения.Ошибка(СообщениеЛога);
		ИначеЕсли УровеньСообщения = УровниЛога.Отладка Тогда
			ЛогСообщения.Отладка(СообщениеЛога);
		ИначеЕсли УровеньСообщения = УровниЛога.КритичнаяОшибка Тогда
			ЛогСообщения.КритичнаяОшибка(СообщениеЛога);
		ИначеЕсли УровеньСообщения = УровниЛога.Предупреждение Тогда
			ЛогСообщения.Предупреждение(СообщениеЛога);
		Иначе
			ЛогСообщения.Отладка(СообщениеЛога)
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ФорматироватьДатуСобытия(Знач ДатаСобытия)
	
	Если ДатаСобытия = Неопределено Тогда
		ДатаСобытия = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЕсли;
		
	ДатаСобытияВМилисекундах = ДатаСобытия;
	ДатаВСекундах = ДатаСобытияВМилисекундах/1000;
	ДатаСобытия = Дата("00010101") + ДатаВСекундах;
	МилисекундыСобытия = Цел((ДатаВСекундах - Цел(ДатаВСекундах))*1000);
	
	ДатаСобытия = СтрШаблон(Формат(ДатаСобытия, "ДФ='yyyy-MM-ddTHH:mm:ss.%1Z'"), Формат(МилисекундыСобытия, "ЧЦ=3; ЧВН="));
	
	Возврат ДатаСобытия;
	
КонецФункции

Функция ПрочитатьФайл(Знач ПутьКФайлу)
	
	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлу, "UTF-8");
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Возврат ТекстФайла;

КонецФункции

Функция СформироватьДанныеДляОбработки(ЗагрузкаРасширенийВыполнена)
	
	ДанныеДляОбработки = Новый Структура();
	ДанныеДляОбработки.Вставить("ОтключитьВсеРасширения", ОтключитьВсеРасширения);
	ДанныеДляОбработки.Вставить("ЗагрузкаОбновленийРасширений", Не ЗагрузкаРасширенийВыполнена);
	ДанныеДляОбработки.Вставить("УровеньСообщений", Лог.Уровень());

	НаборРасширенийЗаписи = Новый Массив();

	Для каждого Расширение Из НаборРасширений Цикл
		
		Если Не ФС.ФайлСуществует(Расширение.ПутьКФайлуОбновления) Тогда
			Лог.КритичнаяОшибка("Не найден файл обновления <%1> для расширения <%2>. Пропущено", Расширение.ПутьКФайлуОбновления, Расширение.Имя);
			Продолжить;
		КонецЕсли;
		
		НовоеРасширение = КопированиеТипа.Скопировать(Расширение);
		НаборРасширенийЗаписи.Добавить(НовоеРасширение);

	КонецЦикла;

	ДанныеДляОбработки.Вставить("НаборРасширений", НаборРасширенийЗаписи);

	Возврат ДанныеДляОбработки;

КонецФункции

Процедура ЗаписатьДанныеДляОбработки(Знач ПутьКФайлу, ДанныеДляОбработки)

	ТекстJSON = ВJson(ДанныеДляОбработки);

	Лог.Отладка("Текст файла для обработки обновления: <%1>", ТекстJSON);

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу);
	ЗаписьТекста.Записать(ТекстJSON);
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Функция ВJson(Знач ДанныеСериализации)
	
	ПарсерJSON = Новый ПарсерJSON;
	Возврат ПарсерJSON.ЗаписатьJSON(ДанныеСериализации);

КонецФункции

Функция ИзJson(ТекстJSON)
	
	Парсер = Новый ПарсерJSON;
	Результат = Парсер.ПрочитатьJSON(ТекстJSON);

	Возврат Результат;

КонецФункции

Функция НоваяИнформацияОбОшибке(Знач Сообщение,
	Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено)
	
	Если ЕстьЗаполненныеПараметры(Параметр1, Параметр2, Параметр3,
		Параметр4, Параметр5, Параметр6, Параметр7, Параметр8, Параметр9) Тогда
		
		Сообщение = СтрШаблон(Сообщение, Параметр1,
		Параметр2, Параметр3, Параметр4, Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	КонецЕсли;
	
	Возврат Сообщение;
	
КонецФункции

Функция ЕстьЗаполненныеПараметры(Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено)
	
	Если НЕ Параметр1 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр2 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр3 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр4 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр5 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр6 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр7 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр8 = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НЕ Параметр9 = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ВыполнитьОбработчикПриСобытия(Знач ИмяСобытия)

	Если ИмяСобытия = "ПередОбновлением" Тогда
		Обработчики = ОбработчикиПередОбновления;
	ИначеЕсли ИмяСобытия = "ПослеОбновления" Тогда
		Обработчики = ОбработчикиПослеОбновления;
	Иначе
		Возврат;
	КонецЕсли;
		
	Для ИндексОбработчика = 0 По Обработчики.ВГраница() Цикл
		
		ТрассировкаЗадач = Новый ТрассировкаЗадач(Лог);
		ДопПоля = Новый Структура("Событие, ИндексОбработчика", ИмяСобытия, ИндексОбработчика);
		ТрассировкаЗадач.Трассировать("Выполнение события", "Выполнение события обновления информационной базы", , , ДопПоля);
		
		Попытка
			
			Таймер = Новый ТаймерВыполнения();
			ЗапускПредприятия = Обработчики[ИндексОбработчика];
			ЗапускПредприятия.Запустить();
	
			ТрассировкаЗадач.Трассировать("Выполнение события", "Выполнено событие обновления информационной базы", "success" , , ДопПоля);
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТрассировкаЗадач.Трассировать("Выполнение события", "Ошибка выполнения события обновления информационной базы", "error" , ИнформацияОбОшибке, ДопПоля);
			
			Если НЕ ЗапускПредприятия.ПродолжитьПриОшибке() Тогда
				ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьОбработку(Знач ПутьКФайлуОбработки, Знач КлючЗапуска = "")
	
	ФайлОбработки = Новый Файл(ПутьКФайлуОбработки);
	
	Если НЕ ФайлОбработки.Существует() Тогда
		ВызватьИсключение НоваяИнформацияОбОшибке("Файл обработки <%1> не найден", ФайлОбработки.ПолноеИмя);
	КонецЕсли;
	
	Лог.Отладка("Выполняю обработку <%1> ключ запуска <%2>", ФайлОбработки.ПолноеИмя, КлючЗапуска);
	ПараметрыЗапускаОбработки = СтрШаблон("/Execute ""%1""", ФайлОбработки.ПолноеИмя);
	РабочийКонфигуратор.ЗапуститьВРежимеПредприятия(КлючЗапуска, РежимУправляемогоПриложения, ПараметрыЗапускаОбработки);

	ВыводКоманды = РабочийКонфигуратор.ВыводКоманды();
	Лог.Отладка("Вывод команды: <%1>", ВыводКоманды);

	Если Не ПустаяСтрока(ВыводКоманды) Тогда
		
		ОшибкаОбработки = "ВнешняяОбработка.ОбновлениеРасширений.МодульОбъекта";
		Если СтрНайти(ВыводКоманды, ОшибкаОбработки) > 0 Тогда
			Описание = СтрШаблон("Ошибка выполнения обработки по причине <%1>", ВыводКоманды);
			// Лог.Информация(Описание)
			ВызватьИсключение Описание;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры