#Использовать "./internal"
#Использовать datetime

Перем ИдентификаторРабочегоПроцесса;
Перем ИдентификаторАгента;
Перем ВыводитьЛогВКонсоль;
Перем ФайлЖурнала;
Перем КаталогЛогов;
Перем ВыводЛогаВRabbitMQ;
Перем Лог;
Перем ДанныеПоСистеме;

Перем КаталогКешаФайлов;
Перем ИспользуетсяОтправкаЛогаВRMQ;

Функция РабочийКаталог(Знач НовыйКаталогЛогов) Экспорт
	КаталогЛогов = НовыйКаталогЛогов;
	Возврат ЭтотОбъект;
КонецФункции

Функция УстановитьКаталогКешаФайлов(Знач НовыйКаталогКешаФайлов) Экспорт
	КаталогКешаФайлов = НовыйКаталогКешаФайлов;
	Возврат ЭтотОбъект;
КонецФункции

Процедура НастроитьВыводЛогаВRabbitMQ(Знач НастройкаRMQ, ЗНач КлючСообщения, Знач КлючМаршрутизации)
	
	Если НастройкаRMQ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Лог.Поля("КлючСообщения", КлючСообщения,
			"КлючМаршрутизации", КлючМаршрутизации).Отладка("Выполняю настройки вывода лога в RMQ");
	ВыводЛогаВRabbitMQ = Новый ВыводЛогаВRabbitMQ(НастройкаRMQ);
	ВыводЛогаВRabbitMQ.УстановитьКлючСоответствия(КлючСообщения);
	ВыводЛогаВRabbitMQ.УстановитьКлючМаршрутизации(КлючМаршрутизации);

	ИспользуетсяОтправкаЛогаВRMQ = Истина;
	
КонецПроцедуры

Функция Идентификатор() Экспорт

	Возврат ИдентификаторРабочегоПроцесса;

КонецФункции

// Выполняет пакетную синхронизацию
//
Процедура Запустить(Знач ПутьКФайлуСообщения) Экспорт
	
	Лог.Информация("Подготовка рабочего процесса");

	ДанныеРабочегоПроцесса = ПрочитатьФайлСообщенияРабочегоПроцесса(ПутьКФайлуСообщения);
	ДанныеСообщенияRMQ = ДанныеРабочегоПроцесса["ДанныеСообщенияRMQ"];
	
	ВходящееСообщениеRMQ = Новый СообщениеRMQ;

	Лог.Отладка("Читаю СообщениеRMQ <%1>", ДанныеСообщенияRMQ);

	ВходящееСообщениеRMQ.ИзСоответствия(ДанныеСообщенияRMQ);
	
	КлючСообщения = ВходящееСообщениеRMQ.ЗначениеПараметра("КлючСообщения");
	КлючМаршрутизации = ВходящееСообщениеRMQ.ЗначениеПараметра("АдресОтвета");
	
	НастройкаRMQ = ДанныеРабочегоПроцесса["НастройкаRMQ"];
	
	НастроитьВыводЛогаВRabbitMQ(НастройкаRMQ, КлючСообщения, КлючМаршрутизации);

	Если ВходящееСообщениеRMQ.КодировкаКонтента = "base64" Тогда
		ДанныеСообщения = ПолучитьСтрокуИзДвоичныхДанных(ВходящееСообщениеRMQ.ДанныеСообщения);
	Иначе
		ДанныеСообщения = ВходящееСообщениеRMQ.ДанныеСообщения;
	КонецЕсли;

	Лог.Отладка("Читаю ДанныеСообщения <%1> (кодировка %2)", ДанныеСообщения, ВходящееСообщениеRMQ.КодировкаКонтента);
	ПараметрыОбновления = РаботаСФайлами.ПрочитатьJSONВОбъект(ДанныеСообщения);
	
	Лог.Отладка("Чтение данных сообщения завершено");
	ВыполнитьДействияРабочегоПроцесса(ПараметрыОбновления);

	УдалитьФайлы(ПутьКФайлуСообщения);

КонецПроцедуры

Процедура ВыполнитьДействияРабочегоПроцесса(Знач ПараметрыОбновления)

	Лог.Информация("Рабочий процесс начал работу");
	СИ = Новый СистемнаяИнформация;

	ОбщиеПоля = Новый Соответствие();
	ОбщиеПоля.Вставить("id", ПараметрыОбновления["id"]);
	ОбщиеПоля.Вставить("agent", ИдентификаторАгента);
	ОбщиеПоля.Вставить("РабочийПроцесс", ИдентификаторРабочегоПроцесса);
	ОбщиеПоля.Вставить("ПользовательОС", СИ.ПользовательОС);
	ОбщиеПоля.Вставить("ИмяКомпьютера", СИ.ИмяКомпьютера);

	ОсновнойЛог = Логирование.ПолучитьЛог("oscript.app.AutoUpdateIB");
	ОсновнойЛог.ДополнительныеПоля(ОбщиеПоля);

	НастройкиОбновления = Новый НастройкаОбновления();	
	ПроцессорОбновления = Новый МенеджерОбновления();

	Если ИспользуетсяОтправкаЛогаВRMQ Тогда
		ОсновнойЛог.ДобавитьСпособВывода(ВыводЛогаВRabbitMQ);
		// НастройкиОбновления.ДобавитьСпособВывода(ВыводЛогаВRabbitMQ);		
	КонецЕсли;

	НастройкиОбновления.УстановитьКешФайлов(КаталогКешаФайлов);
	НастройкиОбновления.Заполнить(ПараметрыОбновления);

	РезультатВыполнения = ПроцессорОбновления.ВыполнитьОбновление(НастройкиОбновления);
	
	Если ИспользуетсяОтправкаЛогаВRMQ Тогда
		ОсновнойЛог.УдалитьСпособВывода(ВыводЛогаВRabbitMQ);
		// НастройкиОбновления.ДобавитьСпособВывода(ВыводЛогаВRabbitMQ);		
	КонецЕсли;

	Если РезультатВыполнения.Выполнено Тогда
		Лог.Информация("Успешно выполнено задание рабочего процесса");
	Иначе
		Лог.Поля("ОписаниеОшибки", РезультатВыполнения.ОписаниеОшибки).КритичнаяОшибка("Рабочий процесс завершил работу с ошибкой");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередатьРабочийПроцессВОбъект(Знач ОбъектПриемник)
	
	Если Не ИдентификаторРабочегоПроцесса = Неопределено Тогда
		ОбъектПриемник.ДобавитьРабочийПроцессВЛог(ИдентификаторРабочегоПроцесса);
	КонецЕсли;

КонецПроцедуры

Функция ПрочитатьНастройкиОбновления(СоответствиеНастройки)
	
	НастройкиОбновления = Новый НастройкаОбновления(Новый УникальныйИдентификатор);
	НастройкиОбновления.УстановитьКаталогКешаФайлов(КаталогКешаФайлов);
	НастройкиОбновления.ИзСоответствия(СоответствиеНастройки);

	Возврат НастройкиОбновления;

КонецФункции

Функция ПрочитатьФайлСообщенияРабочегоПроцесса(Знач ИмяФайлаСообщения)

	Лог.Отладка("Читаю текст сообщения рабочего процесса агента");
	
	ТекстФайлаСообщения = РаботаСФайлами.ПрочитатьФайл(ИмяФайлаСообщения);

	// Лог.Отладка("Текст сообщения <%1>", ТекстФайлаСообщения);
	СообщениеРабочегоПроцесса = Новый СообщениеРабочегоПроцесса;
	СообщениеРабочегоПроцесса.ПрочитатьСообщение(ТекстФайлаСообщения);

	ДанныеРабочегоПроцесса = СообщениеРабочегоПроцесса.ПолучитьДанныеСообщения();

	Возврат ДанныеРабочегоПроцесса;

КонецФункции

Процедура ПриСозданииОбъекта(ВходящийИдентификаторРабочегоПроцесса = Неопределено, ПИдентификаторАгента = Неопределено)

	Если ВходящийИдентификаторРабочегоПроцесса = Неопределено Тогда
		ИдентификаторРабочегоПроцесса = Новый УникальныйИдентификатор;
	Иначе
		ИдентификаторРабочегоПроцесса = ВходящийИдентификаторРабочегоПроцесса;
	КонецЕсли;

	ИдентификаторАгента = ПИдентификаторАгента;

	Если ИдентификаторАгента = Неопределено Тогда
		ИдентификаторАгента = "";
	КонецЕсли;

	ВыводитьЛогВКонсоль = Истина;

	СИ = Новый СистемнаяИнформация;
	ОбщийЛог = Логирование.ПолучитьЛог("oscript.app.AutoUpdateIB");

	ДопПоля = Новый Соответствие();
	ДопПоля.Вставить("Префикс", "worker");
	ДопПоля.Вставить("agent", ИдентификаторАгента);
	ДопПоля.Вставить("РабочийПроцесс", ИдентификаторРабочегоПроцесса);
	ДопПоля.Вставить("ПользовательОС", СИ.ПользовательОС);
	ДопПоля.Вставить("ИмяКомпьютера", СИ.ИмяКомпьютера);

	Лог = ОбщийЛог.ПоляИз(ДопПоля);
	// Лог.УстановитьУровень(УровниЛога.Отладка);

	ИспользуетсяОтправкаЛогаВRMQ = Ложь;

КонецПроцедуры

