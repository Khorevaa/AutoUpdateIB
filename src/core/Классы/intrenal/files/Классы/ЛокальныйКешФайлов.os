#Использовать logos

Перем Лог;

Перем КаталогКешаФайлов;
Перем ИндексХешаФайлов;
Перем ДатаЧтенияКеша;

Функция Получить(ОбработчикПолучения) Экспорт

	ОбновитьКеш();

	ХешФайла = ОбработчикПолучения.ХешФайла();
	
	Лог.Отладка("Получаю значение пути к файлу по хешу <%1> ", ХешФайла);
	ПутьКФайлу = ИндексХешаФайлов[ХешФайла];

	Если ПутьКФайлу = Неопределено Тогда
	
		ОбработчикПолучения.Получить(КаталогКешаФайлов);
	
		ОбновитьКеш();

		ПутьКФайлу = ИндексХешаФайлов[ХешФайла];

		Если ПутьКФайлу = Неопределено Тогда
			ВызватьИсключение СтрШаблон("Ошибка получения файла по хешу <%1>. Файл не существует", ХешФайла);
		КонецЕсли;

		Возврат ПутьКФайлу;

	КонецЕсли;

	Возврат ПутьКФайлу;

КонецФункции

Процедура ОбновитьКеш() Экспорт

	РасширенияФайлов = "*.cf|*.cfu|*.epf";

	ИндексХешаФайлов = Новый Соответствие;

	МассивРасширений = СтрРазделить(РасширенияФайлов, "|");

	Для каждого МаскаПоиска Из МассивРасширений Цикл

		ДобавитьФайлыПоМаске(МаскаПоиска);

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьФайлыПоМаске(МаскаФайлов)
	
	МассивФайлов = НайтиФайлы(КаталогКешаФайлов, МаскаФайлов);

	Лог.Отладка("Найдено файлов <%1> в каталоге <%2> по маске поиска <%3> ",
				МассивФайлов.Количество(),
				КаталогКешаФайлов,
				МаскаФайлов);

	Для каждого ФайлХеша Из МассивФайлов Цикл

		ИмяХешаФайла = ФайлХеша.ИмяБезРасширения;

		Лог.Отладка("Добавляю в кеш файла <%1> - ключ хеша <%2> ", ФайлХеша.ПолноеИмя, ИмяХешаФайла);
	
		ИндексХешаФайлов.Вставить(ИмяХешаФайла, ФайлХеша.ПолноеИмя);

	КонецЦикла;

КонецПроцедуры

Процедура ОчиститьКеш() Экспорт
	
	УдалитьФайлы(КаталогКешаФайлов);

КонецПроцедуры

Функция НужноОбновитьКеш()

	// TODO: Добавить логику чтения даты файла обновления кеша
	Возврат Истина;
	
КонецФункции

Процедура ПриСозданииОбъекта(Каталог)

	КаталогКешаФайлов = Каталог;
	
	Лог = Логирование.ПолучитьЛог("oscript.app.AutoUpdateIB.local-cache");
	Лог.УстановитьУровень(УровниЛога.Отладка);

КонецПроцедуры

