#Использовать fs

Перем ПутьКФайлу;
Перем ХешФайла;
Перем ХешСуммаСтрокой;

Функция ХешФайла() Экспорт
	
	Если ЗначениеЗаполнено(ХешСуммаСтрокой) Тогда
		Возврат ХешСуммаСтрокой;
	КонецЕсли;

	ХешСуммаСтрокой = ХешФайла.ХешСуммаСтрокой();

	Возврат ХешСуммаСтрокой;

КонецФункции

Функция Используется() Экспорт
	Возврат ЗначениеЗаполнено(ПутьКФайлу);
КонецФункции

Процедура Параметры(ВходящиеПараметры) Экспорт
	
	ПутьКФайлу = ВходящиеПараметры["ПутьКФайлу"];

	ХешФайла = Новый ПараметрыХешаФайла();
	ХешФайла.Настроить(ВходящиеПараметры["ХешФайла"]);

КонецПроцедуры

Процедура СверитьХешФайла(ПутьКФайлуСверки)
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.ДобавитьФайл(ПутьКФайлуСверки);
	ТекущийХеш = НРег(ХешированиеДанных.ХешСуммаСтрокой);

	Если НЕ ТекущийХеш = ХешФайла() Тогда
		ТекстСообщения = СтрШаблон(
			"Не совпадают хеши файлов:
			|Контрольная сумма ожидаемая: %1
			|Контрольная сумма файла: %2",
			ХешФайла(),
			ТекущийХеш
		);
		ИнфИсключение = Новый ИнформацияОбОшибке(ТекстСообщения, ХешФайла());
		ВызватьИсключение ИнфИсключение;
	КонецЕсли;

КонецПроцедуры

Процедура Получить(Знач Каталог) Экспорт
	
	ФайлПолучения = Новый Файл(ПутьКФайлу);
	
	Если ФайлПолучения.ЭтоКаталог() Тогда
		ТекстСообщения = СтрШаблон(
			"Передан каталог вместо файла: %1",
			ФайлПолучения.ПолноеИмя
		);
		ИнфИсключение = Новый ИнформацияОбОшибке(ТекстСообщения, ХешФайла());
		ВызватьИсключение ИнфИсключение;
	
	КонецЕсли;

	ПолучитьФайлВКаталог(Каталог, ФайлПолучения);

КонецПроцедуры

Процедура ПолучитьФайлВКаталог(Знач Каталог, ФайлПолучения)
	
	ФайлПриемник = ФС.ПолныйПуть(ОбъединитьПути(Каталог, ХешФайла()) + ФайлПолучения.Расширение);
	КопироватьФайл(ФайлПолучения.ПолноеИмя, ФайлПриемник);

	СверитьХешФайла(ФайлПриемник);

КонецПроцедуры

Процедура ОписаниеПараметров(Знач Конструктор) Экспорт
	
	Конструктор
		.ПолеСтрока("ПутьКФайлу ПутьККаталогу path", "")
		.ПолеОбъект("ХешФайла hash", Новый ПараметрыХешаФайла)
		;

КонецПроцедуры

Процедура ПриСозданииОбъекта()
	ПутьКФайлу = "";
КонецПроцедуры