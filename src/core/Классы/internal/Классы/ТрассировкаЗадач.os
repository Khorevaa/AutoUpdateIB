#Использовать "../timer"

Перем ИндексЗадач;
Перем Лог;

Процедура ПриСозданииОбъекта(Знач ПЛог)
	Лог = ПЛог;
	ИндексЗадач = Новый Соответствие();
КонецПроцедуры

Процедура Трассировать(Знач ИмяЗадачи, Знач Сообщение = "", Знач Статус = "do", Знач Ошибка = Неопределено,  ДопПоля = Неопределено) Экспорт

	Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
		Сообщение = СтрШаблон("Стартована задача <%1>", ИмяЗадачи);
	КонецЕсли;
	
	ОписаниеТрассировки = Новый Соответствие();
	
	ТаймерЗадачи = ИндексЗадач[ИмяЗадачи];
	ОписаниеТрассировки.Вставить("task", ИмяЗадачи);
	ОписаниеТрассировки.Вставить("status", Статус);
	Если Статус = "error" Тогда
		ОписаниеТрассировки.Вставить("error", Ошибка);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТаймерЗадачи) Тогда
		ТаймерЗадачи = Новый ТаймерВыполнения();
		ИндексЗадач.Вставить(ИмяЗадачи, ТаймерЗадачи);
	Иначе
		ОписаниеТрассировки.Вставить("duration", ТаймерЗадачи.ВремяЗамера());
	КонецЕсли;

	Если ЗначениеЗаполнено(ДопПоля) Тогда
		
		Для каждого ДопПоле Из ДопПоля Цикл
			ОписаниеТрассировки.Вставить(ДопПоле.Ключ, ДопПоле.Значение);
		КонецЦикла;

	КонецЕсли;

	Если Статус = "error" Тогда
		Лог.ПоляИз(ОписаниеТрассировки).Ошибка(Сообщение);
	Иначе
		Лог.ПоляИз(ОписаниеТрассировки).Информация(Сообщение);
	КонецЕсли;

КонецПроцедуры

Процедура Очистить() Экспорт
	ИндексЗадач = Новый Соответствие();
КонецПроцедуры