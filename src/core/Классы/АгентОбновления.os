#Использовать "./internal"
#Использовать logos
#Использовать json
#Использовать datetime

Перем Лог;
Перем ОбщийЛог;

Перем МаксимальноеКоличествоРабочихПроцессов;
Перем КомандаПроцессаАгента;
Перем ПутьКФайлуСкрипта;

Перем ТаймерПовторенияОпроса;
Перем ТаймаутРабочегоПроцесса;
Перем ПараллельныеПроцессы;
Перем СчетчикПроцессов;
Перем РабочийКаталогПроцессов;
Перем ДанныеПоСистеме;

Перем НастройкаRMQ;
Перем КлиентRMQ;
Перем Идентификатор;
Перем ПользовательОС;
Перем ИмяКомпьютера;
Перем Отладка;

Процедура УстановитьКоличествоРабочихПроцессов(Знач НовоеМаксимальноеКоличествоРабочихПроцессов) Экспорт
	
	Если НовоеМаксимальноеКоличествоРабочихПроцессов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МаксимальноеКоличествоРабочихПроцессов = НовоеМаксимальноеКоличествоРабочихПроцессов;

КонецПроцедуры

Процедура УстановитьОткладку(Знач ПОтладка) Экспорт
	Отладка = ПОтладка;
КонецПроцедуры

Процедура УстановитьТаймерОпроса(Знач НовыйТаймерПовторенияОпроса) Экспорт
	ТаймерПовторенияОпроса = НовыйТаймерПовторенияОпроса;
КонецПроцедуры

Процедура УстановитьТаймаутРабочегоПроцесса(Знач НовыйТаймаут) Экспорт
	ТаймаутРабочегоПроцесса = НовыйТаймаут;
КонецПроцедуры

Процедура УстановитьРабочийКаталогПроцессов(Знач ПутьККаталогу) Экспорт
	РабочийКаталогПроцессов = ПутьККаталогу;
КонецПроцедуры

Процедура НастроитьRMQ(Знач НовыеНастройкиПодключенияRMQ) Экспорт
	
	НастройкаRMQ = НовыеНастройкиПодключенияRMQ;

	КлиентRMQ = Новый КлиентRMQ();
	КлиентRMQ.УстановитьНастройкиПодключения(НастройкаRMQ);
	КлиентRMQ.УстановитьВиртуальныйХост(НастройкаRMQ.ВиртуальныйХост);

КонецПроцедуры

Процедура Запустить() Экспорт
	
	ПроверитьВозможностьЗапуска();
	ВыводЛогаВRabbitMQ = Новый ВыводЛогаВRabbitMQ(НастройкаRMQ);
	
	Если ЗначениеЗаполнено(НастройкаRMQ.ИмяОчередиЛогов) Тогда
		ВыводЛогаВRabbitMQ.УстановитьКлючМаршрутизации(НастройкаRMQ.ИмяОчередиЛогов);
	Иначе
		ВыводЛогаВRabbitMQ.УстановитьТочкаОбмена(НастройкаRMQ.ТочкаОбмена);
	КонецЕсли;

	ОбщийЛог.ДобавитьСпособВывода(ВыводЛогаВRabbitMQ);

	Пока Истина Цикл

		ОчиститьЗавешенныеПроцессы();

		ОпроситьПроцессыПоТаймауту();

		КоличествоСообщенийВОчереди = КлиентRMQ.КоличествоСообщенийВОчереди(НастройкаRMQ.ИмяОчереди);

		Лог.Поля("КоличествоСообщенийВОчереди", КоличествоСообщенийВОчереди).Информация("Получаю сообщения из RMQ");

		ЗапуститьАгентовПриНеобходимости(КоличествоСообщенийВОчереди);

		Если ТаймерПовторенияОпроса <= 0 
			И КоличествоСообщенийВОчереди = 0 Тогда

			ОжидатьЗавершенияПроцессов();

			Прервать;

		Иначе

			ЛокальныйТаймер = ТаймерПовторенияОпроса;

			Если ЛокальныйТаймер = 0 Тогда

				ЛокальныйТаймер = 10;

			КонецЕсли;

			Приостановить(ЛокальныйТаймер * 1000);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры


Процедура ОжидатьЗавершенияПроцессов()

	Пока ПараллельныеПроцессы.Количество() > 0 Цикл

		ОпроситьПроцессыПоТаймауту();
			
		Приостановить(10 * 1000);

	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьВозможностьЗапуска()
	
	Если КлиентRMQ = Неопределено Тогда
		ВызватьИсключение "Не настроена очередь RabbitMQ";
	КонецЕсли;

КонецПроцедуры

Процедура ЗапуститьАгентовПриНеобходимости(Знач КоличествоСообщенийВОчереди)

	Если КоличествоСообщенийВОчереди = 0 Тогда
		Возврат;
	КонецЕсли;

	ФайлИсточника = Новый Файл(ПутьКФайлуСкрипта);

	Если ФайлИсточника.Расширение = ".os" Тогда
		КомандаПроцессаАгента = "oscript";
	Иначе
		КомандаПроцессаАгента = ПутьКФайлуСкрипта;
	КонецЕсли;

	КоличествоПроцессов = Мин(МаксимальноеКоличествоРабочихПроцессов, КоличествоСообщенийВОчереди);
	ЗапуститьПаралельно(КоличествоПроцессов);

КонецПроцедуры

Процедура ОпроситьПроцессыПоТаймауту()

	Если ПараллельныеПроцессы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Лог.Поля("КоличествоРабочихПроцессов", ПараллельныеПроцессы.Количество()).Информация("Опрос рабочих процессов по таймауту");

	ИзмененияЛогов = ПолучитьДатыИзмененияЛогов();

	Для каждого КлючЗначение Из ИзмененияЛогов Цикл
		
		ИдентификаторРабочегоПроцесса = КлючЗначение.Ключ;
		ДатаИзменения = КлючЗначение.Значение;

		Если ДатаИзменения + ТаймаутРабочегоПроцесса < ТекущаяДата() Тогда
			ОписаниеПроцесса = ПараллельныеПроцессы[ИдентификаторРабочегоПроцесса];
			Лог.Поля("РабочийПроцесс", ОписаниеПроцесса.Идентификатор,
					"ВремяЗапуска", ОписаниеПроцесса.ВремяЗапуска,
					"ПутьКФайлуЛога", ОписаниеПроцесса.ПутьКФайлуЛога
					).Предупреждение("Завершение рабочего процесса по таймауту");
			ЗавершитьРабочегоПроцесса(ОписаниеПроцесса.Процесс);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ЗавершитьРабочегоПроцесса(Знач РабочийПроцесс)

	Если Не РабочийПроцесс.Завершен Тогда
		РабочийПроцесс.Завершить();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДатыИзмененияЛогов()
	
	ИзмененияЛогов = Новый Соответствие;

	Для каждого ПроцессКоманды Из ПараллельныеПроцессы Цикл
		
		ОписаниеПроцесса = ПроцессКоманды.Значение;
		ПутьКФайлуЛога = ОписаниеПроцесса.ПутьКФайлуЛога;
		ДатаИзмененияЛогаПроцесса = ПолучитьДатуИзмененияФайла(ПутьКФайлуЛога);

		ИзмененияЛогов.Вставить(ПроцессКоманды.Ключ, ДатаИзмененияЛогаПроцесса);

	КонецЦикла;

	Возврат ИзмененияЛогов;

КонецФункции

Функция ПолучитьДатуИзмененияФайла(Знач ПутьКФайлуЛога)
	
	ФайлЛога = Новый Файл(ПутьКФайлуЛога);
	
	Если Не ФайлЛога.Существует() Тогда
		Возврат Дата("00010101");
	КонецЕсли;
	
	Возврат ФайлЛога.ПолучитьВремяИзменения();

КонецФункции

Процедура ЗапуститьПаралельно(КоличествоПроцессов = 0)

	ОчиститьЗавешенныеПроцессы();

	КоличествоРаботающихПроцессов = ПараллельныеПроцессы.Количество();

	Если КоличествоРаботающихПроцессов >= КоличествоПроцессов Тогда
		Возврат;
	КонецЕсли;

	Если КоличествоПроцессов - КоличествоРаботающихПроцессов = 0 Тогда
		Возврат;
	КонецЕсли;

	ИнтервалЗапускаПроцессов = 1000*10; // 1 секунда
	Лог.Отладка("Запуск рабочих <%1> процессов", КоличествоПроцессов - КоличествоРаботающихПроцессов);

	Для Счетчик = 1 По КоличествоПроцессов - КоличествоРаботающихПроцессов Цикл
		
		МассивСообщенийRMQ = КлиентRMQ.ПолучитьСообщениеИзОчереди(НастройкаRMQ.ИмяОчереди, 1);

		Если МассивСообщенийRMQ.Количество() = 0  Тогда
			Возврат;
		КонецЕсли;

		СообщениеRMQ = МассивСообщенийRMQ[0];

		Лог.Отладка("Ключ маршрутизации <%1>", СообщениеRMQ.КлючМаршрутизации);
		Лог.Отладка("Ключ сообщения <%1>", СообщениеRMQ.ЗначениеПараметра("КлючСообщения"));

		СчетчикПроцессов = СчетчикПроцессов + 1;

		ИдентификаторРабочегоПроцесса = СтрШаблон("worker_%1_%2", СчетчикПроцессов, Формат(ТекущаяДата(), "ДФ=yyyyMMdd_HHmmss"));

		ИмяФайлаСообщения = ПодготовитьФайлСообщенияНаРабочийПроцесс(ИдентификаторРабочегоПроцесса, СообщениеRMQ);

		Лог.Информация("Запускаю процесс <%1>", ИдентификаторРабочегоПроцесса);
		
		Команда = Новый ПараллельнаяКоманда;
		Команда.УстановитьКоманду(КомандаПроцессаАгента);
		Если КомандаПроцессаАгента = "oscript" Тогда
			//Команда.ДобавитьПараметр("-encoding=utf-8");
			Команда.ДобавитьПараметр(ПутьКФайлуСкрипта);
		КонецЕсли;
		
		Если Отладка Тогда
			Команда.ДобавитьПараметр("-v");
		КонецЕсли;

		ПутьКФайлуЛогаПроцесса = ОбъединитьПути(РабочийКаталогПроцессов, ИдентификаторРабочегоПроцесса + ".log");

		Команда.ДобавитьПараметр("--log-file");
		Команда.ДобавитьПараметр(ПутьКФайлуЛогаПроцесса);
	
		Команда.ДобавитьПараметр("worker");
		Команда.ДобавитьПараметр("--agent-id");
		Команда.ДобавитьПараметр(Идентификатор);
		
		Команда.ДобавитьПараметр(ИмяФайлаСообщения);
		Команда.ДобавитьПараметр(ИдентификаторРабочегоПроцесса);
		
		ПроцессКоманды = Команда.ЗапуститьПроцесс();

		ОписаниеПроцесса = Новый Структура();
		ОписаниеПроцесса.Вставить("Идентификатор", ИдентификаторРабочегоПроцесса);
		ОписаниеПроцесса.Вставить("ПутьКФайлуЛога", ПутьКФайлуЛогаПроцесса);
		ОписаниеПроцесса.Вставить("СообщениеRMQ", СообщениеRMQ);
		ОписаниеПроцесса.Вставить("Команда", Команда);
		ОписаниеПроцесса.Вставить("Процесс", ПроцессКоманды);
		ОписаниеПроцесса.Вставить("ВремяЗапуска", ТекущаяДата());

		ПараллельныеПроцессы.Вставить(ИдентификаторРабочегоПроцесса, ОписаниеПроцесса);

		Приостановить(ИнтервалЗапускаПроцессов);

	КонецЦикла;

КонецПроцедуры

Функция ПодготовитьФайлСообщенияНаРабочийПроцесс(ИдентификаторРабочегоПроцесса, СообщениеRMQ)

	ИмяФайлаСообщения = ВременныеФайлы.НовоеИмяФайла("message");
	Лог.Поля("Файл", ИмяФайлаСообщения, "РабочийПроцесс", ИдентификаторРабочегоПроцесса).Информация("Подготовка данных для рабочего процесса");

	ДанныеНаРабочийПроцесс = Новый Структура();
	ДанныеСообщенияRMQ = СообщениеRMQ.ВСоответствие();

	ДанныеНаРабочийПроцесс.Вставить("НастройкаRMQ", НастройкаRMQ);
	ДанныеНаРабочийПроцесс.Вставить("ДанныеСообщенияRMQ", ДанныеСообщенияRMQ);

	ПарсерJSON = Новый ПарсерJSON;
	ТекстФайлаСообщения = ПарсерJSON.ЗаписатьJSON(ДанныеНаРабочийПроцесс);
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайлаСообщения);
	ЗаписьТекста.Записать(ТекстФайлаСообщения);
	ЗаписьТекста.Закрыть();

	Возврат ИмяФайлаСообщения;

КонецФункции

Процедура ОчиститьЗавешенныеПроцессы()

	Если ПараллельныеПроцессы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Лог.Поля("КоличествоРабочихПроцессов", ПараллельныеПроцессы.Количество()).Информация("Обрабатываю список рабочих процессов");

	МассивОчистки = Новый Массив;

	Для каждого ПроцессКоманды Из ПараллельныеПроцессы Цикл
		
		ОписаниеПроцесса = ПроцессКоманды.Значение;
		СистемныйПроцесс = ОписаниеПроцесса.Процесс;
		
		Если Не СистемныйПроцесс.Завершен Тогда
			Продолжить;
		КонецЕсли;
		
		МассивОчистки.Добавить(ПроцессКоманды.Ключ);
		КодВозвратаПроцесса = СистемныйПроцесс.КодВозврата;

		Если КодВозвратаПроцесса <> 0 Тогда
			Лог.Поля("РабочийПроцесс", ПроцессКоманды.Ключ, "КодВозврата", КодВозвратаПроцесса).Предупреждение("Процесс завершился не с <0< кодом")
		КонецЕсли;

	КонецЦикла;

	Лог.Поля("КоличествоРабочихПроцессов", МассивОчистки.Количество()).Информация("Очистка рабочих процессов");

	Для каждого ОчищаемыйПроцесс Из МассивОчистки Цикл
		ПараллельныеПроцессы.Удалить(ОчищаемыйПроцесс);
	КонецЦикла;

	Если ПараллельныеПроцессы.Количество() = 0 Тогда
		СчетчикПроцессов = 0; 
	КонецЕсли;

КонецПроцедуры

Процедура ПриСозданииОбъекта(Знач ПИдентификатор)
	
	СИ = Новый СистемнаяИнформация;
	
	Если ПИдентификатор = Неопределено Тогда
		Идентификатор = Новый УникальныйИдентификатор();
	Иначе
		Идентификатор = ПИдентификатор;
	КонецЕсли;

	МаксимальноеКоличествоРабочихПроцессов = СИ.КоличествоПроцессоров * 2 - 1;

	ТаймерПовторенияОпроса = 0;
	ТаймаутРабочегоПроцесса = 60 * 60 * 6; // 6 Часов

	ПараллельныеПроцессы = Новый Соответствие;
	
	СИ = Новый СистемнаяИнформация;
	ПользовательОС = СИ.ПользовательОС;
	ИмяКомпьютера = СИ.ИмяКомпьютера;
	
	ОбщийЛог =  Логирование.ПолучитьЛог("oscript.app.AutoUpdateIB");
	
	ДопПоля = Новый Соответствие();
	ДопПоля.Вставить("agent", Идентификатор);
	ДопПоля.Вставить("ПользовательОС", СИ.ПользовательОС);
	ДопПоля.Вставить("ИмяКомпьютера", СИ.ИмяКомпьютера);
	
	ОбщийЛог.ДополнительныеПоля(ДопПоля);

	Лог = ОбщийЛог.Поля("Префикс", "agent");

	ПутьКФайлуСкрипта = СтартовыйСценарий().Источник;
	СчетчикПроцессов = 0;

	Отладка = Ложь;

КонецПроцедуры


