#Использовать logos
#Использовать v8runner
#Использовать tempfiles

Перем КаталогПроекта;
Перем Лог;

Процедура ПолезнаяРабота()

	КаталогРасширений = ОбъединитьПути(КаталогПроекта, "tests", "fixtures","cfe");
	КаталогПоставок = ОбъединитьПути(КаталогПроекта, "tests", "fixtures","distr");

	ПосчитатьХешСуммыФайловВКаталоге(КаталогРасширений, "*.cfe");
	ПосчитатьХешСуммыФайловВКаталоге(КаталогПоставок, "*.*");

КонецПроцедуры

Процедура ПосчитатьХешСуммыФайловВКаталоге(КаталогФайлов, МаскаПоиска)
	
	ФайлиДляРасчетаСумм = НайтиФайлы(КаталогФайлов, МаскаПоиска, Истина);

	Для каждого Файл Из ФайлиДляРасчетаСумм Цикл
		
		Если Файл.ЭтоКаталог() 
			ИЛИ Файл.Расширение = ".md5" Тогда
			Продолжить;
		КонецЕсли;

		ДвоичныеДанныеФайла = Новый ДвоичныеДанные(Файл.ПолноеИмя);
		ХешФайла = ПолучитьХешФайла(ДвоичныеДанныеФайла);

		ФайлДанныхХеша = Файл.ПолноеИмя + ".md5";
		ЗаписатьФайл(ФайлДанныхХеша, ХешФайла);

	КонецЦикла;

КонецПроцедуры

Процедура ЗаписатьФайл(Знач ПутьКФайлу, Знач СодержаниеФайла)
	
	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу);
	ЗаписьТекста.Записать(СодержаниеФайла);
	ЗаписьТекста.Закрыть();
	
КонецПроцедуры

Функция ПолучитьХешФайла(ДвоичныеДанныеФайла, Знач ИмяХешФункции = "MD5")
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция[ИмяХешФункции]);
	ХешированиеДанных.Добавить(ДвоичныеДанныеФайла);
	ХешСуммаСтрокой = ХешированиеДанных.ХешСуммаСтрокой;
	ХешированиеДанных.Очистить();
	
	Возврат ХешСуммаСтрокой;
	
КонецФункции

КаталогПроекта = ОбъединитьПути(ТекущийСценарий().Каталог, "..");

Лог = Логирование.ПолучитьЛог("oscript.tasks");

ПолезнаяРабота();
